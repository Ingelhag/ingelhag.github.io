<html>
    <head>
        <title>Fireball</title>
        <script src="//code.jquery.com/jquery-1.11.3.min.js"></script>
        <script src="//code.jquery.com/jquery-migrate-1.2.1.min.js"></script>
        <script src="https://cdnjs.cloudflare.com/ajax/libs/three.js/r73/three.min.js"></script>

        <!-- Latest compiled and minified CSS -->
        <link rel="stylesheet" href="https://maxcdn.bootstrapcdn.com/bootstrap/3.3.6/css/bootstrap.min.css" integrity="sha384-1q8mTJOASx8j1Au+a5WDVnPi2lkFfwwEAa8hDDdjZlpLegxhjVME1fgjWPGmkzs7" crossorigin="anonymous">

        <!-- Optional theme -->
        <link rel="stylesheet" href="https://maxcdn.bootstrapcdn.com/bootstrap/3.3.6/css/bootstrap-theme.min.css" integrity="sha384-fLW2N01lMqjakBkx3l/M9EahuwpSfeNvV63J5ezn3uZzapT0u7EYsXMjQV+0En5r" crossorigin="anonymous">

        <!-- Latest compiled and minified JavaScript -->
        <script src="https://maxcdn.bootstrapcdn.com/bootstrap/3.3.6/js/bootstrap.min.js" integrity="sha384-0mSbJDEHialfmuBBQP6A4Qrprq5OVfW37PRR3j5ELqxss1yVqOtnepnHVP9aJ7xS" crossorigin="anonymous"></script>

        <script src="stats.min.js"></script>
        <script src="OrbitControls.js"></script>
    </head>
    <body style="background-color:black; color:white;">
        <div class="row">
            <div class="col-md-12" style="text-align:center">
                <h1>Procedural Fireball</h1>
            </div>
        </div>
        <div class="row">
            <div class="col-md-2">
                <h3>Control Panel</h3>
                <div style="text-align:center;">
                    <p>East - West Wind </p>
                    <input type="range" id="west-east-wind" min="-5.0" max="5.0" step="0.1">
                    <p>South - North Wind</p>
                    <input type="range" id="south-north-wind" min="-5.0" max="5.0" step="0.1">
                </div>
            </div>
            <div class="col-md-10" id="fireball" style="height:640; width:640;">
            </div>
        </div>

        <!-- Shaders -->
        <script type="x-shader/x-vertex" id="fireVertexshader">

            #ifdef GL_ES
                precision highp float;
            #endif

            varying vec3 st;
            uniform float time;
            uniform float ewWind;
            uniform float snWind; 

            //
            // Description : Array and textureless GLSL 2D/3D/4D simplex 
            //               noise functions.
            //      Author : Ian McEwan, Ashima Arts.
            //  Maintainer : ijm
            //     Lastmod : 20110822 (ijm)
            //     License : Copyright (C) 2011 Ashima Arts. All rights reserved.
            //               Distributed under the MIT License. See LICENSE file.
            //               https://github.com/ashima/webgl-noise
            // 

            vec3 mod289(vec3 x) {
            return x - floor(x * (1.0 / 289.0)) * 289.0;
            }

            vec4 mod289(vec4 x) {
            return x - floor(x * (1.0 / 289.0)) * 289.0;
            }

            vec4 permute(vec4 x) {
            return mod289(((x*34.0)+1.0)*x);
            }

            vec4 taylorInvSqrt(vec4 r)
            {
            return 1.79284291400159 - 0.85373472095314 * r;
            }

            float snoise(vec3 v)
            { 
            const vec2  C = vec2(1.0/6.0, 1.0/3.0) ;
            const vec4  D = vec4(0.0, 0.5, 1.0, 2.0);

            // First corner
            vec3 i  = floor(v + dot(v, C.yyy) );
            vec3 x0 =   v - i + dot(i, C.xxx) ;

            // Other corners
            vec3 g = step(x0.yzx, x0.xyz);
            vec3 l = 1.0 - g;
            vec3 i1 = min( g.xyz, l.zxy );
            vec3 i2 = max( g.xyz, l.zxy );

            //   x0 = x0 - 0.0 + 0.0 * C.xxx;
            //   x1 = x0 - i1  + 1.0 * C.xxx;
            //   x2 = x0 - i2  + 2.0 * C.xxx;
            //   x3 = x0 - 1.0 + 3.0 * C.xxx;
            vec3 x1 = x0 - i1 + C.xxx;
            vec3 x2 = x0 - i2 + C.yyy; // 2.0*C.x = 1/3 = C.y
            vec3 x3 = x0 - D.yyy;      // -1.0+3.0*C.x = -0.5 = -D.y

            // Permutations
            i = mod289(i); 
            vec4 p = permute( permute( permute( 
            i.z + vec4(0.0, i1.z, i2.z, 1.0 ))
            + i.y + vec4(0.0, i1.y, i2.y, 1.0 )) 
            + i.x + vec4(0.0, i1.x, i2.x, 1.0 ));

            // Gradients: 7x7 points over a square, mapped onto an octahedron.
            // The ring size 17*17 = 289 is close to a multiple of 49 (49*6 = 294)
            float n_ = 0.142857142857; // 1.0/7.0
            vec3  ns = n_ * D.wyz - D.xzx;

            vec4 j = p - 49.0 * floor(p * ns.z * ns.z);  //  mod(p,7*7)

            vec4 x_ = floor(j * ns.z);
            vec4 y_ = floor(j - 7.0 * x_ );    // mod(j,N)

            vec4 x = x_ *ns.x + ns.yyyy;
            vec4 y = y_ *ns.x + ns.yyyy;
            vec4 h = 1.0 - abs(x) - abs(y);

            vec4 b0 = vec4( x.xy, y.xy );
            vec4 b1 = vec4( x.zw, y.zw );

            //vec4 s0 = vec4(lessThan(b0,0.0))*2.0 - 1.0;
            //vec4 s1 = vec4(lessThan(b1,0.0))*2.0 - 1.0;
            vec4 s0 = floor(b0)*2.0 + 1.0;
            vec4 s1 = floor(b1)*2.0 + 1.0;
            vec4 sh = -step(h, vec4(0.0));

            vec4 a0 = b0.xzyw + s0.xzyw*sh.xxyy ;
            vec4 a1 = b1.xzyw + s1.xzyw*sh.zzww ;

            vec3 p0 = vec3(a0.xy,h.x);
            vec3 p1 = vec3(a0.zw,h.y);
            vec3 p2 = vec3(a1.xy,h.z);
            vec3 p3 = vec3(a1.zw,h.w);

            //Normalise gradients
            vec4 norm = taylorInvSqrt(vec4(dot(p0,p0), dot(p1,p1), dot(p2, p2), dot(p3,p3)));
            p0 *= norm.x;
            p1 *= norm.y;
            p2 *= norm.z;
            p3 *= norm.w;

            // Mix final noise value
            vec4 m = max(0.6 - vec4(dot(x0,x0), dot(x1,x1), dot(x2,x2), dot(x3,x3)), 0.0);
            m = m * m;
            return 42.0 * dot( m*m, vec4( dot(p0,x0), dot(p1,x1), 
                            dot(p2,x2), dot(p3,x3) ) );
            }

            float clamps(float value, float minValue, float maxValue) {
                return max(minValue, min(value, maxValue));
            }
            float smoothsteps(float edge0, float edge1, float value) {
                float t = clamps((value - edge0) / (edge1 - edge0), 0.0, 1.0);
                return t * t * (3.0 - 2.0 * t);
            }

            void main(void) {

                float displacement = 0.0;
                float t = time;

                displacement += (1.0/2.0) * snoise(vec3(0.25    * (position.x + t*0.2), 0.5  * (position.y + t*0.5), 0.25 * position.z));
                displacement += (1.0/4.0) * snoise(vec3(0.5     * (position.x + t*0.2), 1.0  * (position.y + t*0.5), 0.5 * position.z));
                displacement += (1.0/8.0) * snoise(vec3(1.0     * (position.x + t*0.2), 2.0  * (position.y + t*0.5), 1.0 * position.z));
                displacement += (1.0/16.0) * snoise(vec3(2.0    * (position.x + t*0.2), 4.0  * (position.y + t*0.5), 2.0 * position.z));
                displacement += (1.0/32.0) * snoise(vec3(4.0    * (position.x + t*0.2), 8.0  * (position.y + t*0.5), 4.0 * position.z));

                // move the position along the normal and transform it
                vec3 newPosition = position + normal * displacement;

                newPosition.y *= (1.5);

                newPosition.x *= smoothsteps(6.0, -2.0, newPosition.y);
                newPosition.z *= smoothsteps(6.0, -2.0, newPosition.y);

                // Wind to the left
                float windspeedX = 1.0 + 0.1*max(0.0,position.y);
                newPosition.x += (ewWind != 0.0)?(ewWind*(windspeedX*windspeedX-1.0)):0.0;

                // Wind to the left
                float windspeedZ = 1.0 + 0.1*max(0.0,position.y);
                newPosition.z += (snWind != 0.0)?(snWind*(windspeedZ*windspeedZ-1.0)):0.0;

                st = newPosition;
                gl_Position = projectionMatrix * modelViewMatrix * vec4( newPosition, 1.0 );   
            }
        </script>

        <script type="x-shader/x-fragment" id="fireFragmentshader">

            #ifdef GL_ES
                precision highp float;
            #endif

            varying vec3 st; // Texcoords
            uniform float time; 
            uniform float ewWind;
            uniform float snWind;

            vec4 mod289(vec4 x) {
                return x - floor(x * (1.0 / 289.0)) * 289.0; 
            }

            float mod289(float x) {
                return x - floor(x * (1.0 / 289.0)) * 289.0; 
            }

            vec4 permute(vec4 x) {
                return mod289(((x*34.0)+1.0)*x); 
            }

            float permute(float x) {
                return mod289(((x*34.0)+1.0)*x);
            }

            vec4 taylorInvSqrt(vec4 r) {
                return 1.79284291400159 - 0.85373472095314 * r;
            }

            float taylorInvSqrt(float r) {
                return 1.79284291400159 - 0.85373472095314 * r;
            }

            vec4 grad4(float j, vec4 ip) {
            const vec4 ones = vec4(1.0, 1.0, 1.0, -1.0);
            vec4 p,s;

            p.xyz = floor( fract (vec3(j) * ip.xyz) * 7.0) * ip.z - 1.0;
            p.w = 1.5 - dot(abs(p.xyz), ones.xyz);
            s = vec4(lessThan(p, vec4(0.0)));
            p.xyz = p.xyz + (s.xyz*2.0 - 1.0) * s.www; 

            return p;
            }

            // (sqrt(5) - 1)/4 = F4, used once below
            #define F4 0.309016994374947451

            float snoise(vec4 v) {
                const vec4  C = vec4( 0.138196601125011,  // (5 - sqrt(5))/20  G4
                0.276393202250021,  // 2 * G4
                0.414589803375032,  // 3 * G4
                -0.447213595499958); // -1 + 4 * G4

                // First corner
                vec4 i  = floor(v + dot(v, vec4(F4)) );
                vec4 x0 = v -   i + dot(i, C.xxxx);

                // Other corners

                // Rank sorting originally contributed by Bill Licea-Kane, AMD (formerly ATI)
                vec4 i0;
                vec3 isX = step( x0.yzw, x0.xxx );
                vec3 isYZ = step( x0.zww, x0.yyz );
                //  i0.x = dot( isX, vec3( 1.0 ) );
                i0.x = isX.x + isX.y + isX.z;
                i0.yzw = 1.0 - isX;
                //  i0.y += dot( isYZ.xy, vec2( 1.0 ) );
                i0.y += isYZ.x + isYZ.y;
                i0.zw += 1.0 - isYZ.xy;
                i0.z += isYZ.z;
                i0.w += 1.0 - isYZ.z;

                // i0 now contains the unique values 0,1,2,3 in each channel
                vec4 i3 = clamp( i0, 0.0, 1.0 );
                vec4 i2 = clamp( i0-1.0, 0.0, 1.0 );
                vec4 i1 = clamp( i0-2.0, 0.0, 1.0 );

                //  x0 = x0 - 0.0 + 0.0 * C.xxxx
                //  x1 = x0 - i1  + 1.0 * C.xxxx
                //  x2 = x0 - i2  + 2.0 * C.xxxx
                //  x3 = x0 - i3  + 3.0 * C.xxxx
                //  x4 = x0 - 1.0 + 4.0 * C.xxxx
                vec4 x1 = x0 - i1 + C.xxxx;
                vec4 x2 = x0 - i2 + C.yyyy;
                vec4 x3 = x0 - i3 + C.zzzz;
                vec4 x4 = x0 + C.wwww;

                // Permutations
                i = mod289(i); 
                float j0 = permute( permute( permute( permute(i.w) + i.z) + i.y) + i.x);
                vec4 j1 = permute( permute( permute( permute (
                i.w + vec4(i1.w, i2.w, i3.w, 1.0 ))
                + i.z + vec4(i1.z, i2.z, i3.z, 1.0 ))
                + i.y + vec4(i1.y, i2.y, i3.y, 1.0 ))
                + i.x + vec4(i1.x, i2.x, i3.x, 1.0 ));

                // Gradients: 7x7x6 points over a cube, mapped onto a 4-cross polytope
                // 7*7*6 = 294, which is close to the ring size 17*17 = 289.
                vec4 ip = vec4(1.0/294.0, 1.0/49.0, 1.0/7.0, 0.0) ;

                vec4 p0 = grad4(j0,   ip);
                vec4 p1 = grad4(j1.x, ip);
                vec4 p2 = grad4(j1.y, ip);
                vec4 p3 = grad4(j1.z, ip);
                vec4 p4 = grad4(j1.w, ip);

                // Normalise gradients
                vec4 norm = taylorInvSqrt(vec4(dot(p0,p0), dot(p1,p1), dot(p2, p2), dot(p3,p3)));
                p0 *= norm.x;
                p1 *= norm.y;
                p2 *= norm.z;
                p3 *= norm.w;
                p4 *= taylorInvSqrt(dot(p4,p4));

                // Mix contributions from the five corners
                vec3 m0 = max(0.6 - vec3(dot(x0,x0), dot(x1,x1), dot(x2,x2)), 0.0);
                vec2 m1 = max(0.6 - vec2(dot(x3,x3), dot(x4,x4)            ), 0.0);
                m0 = m0 * m0;
                m1 = m1 * m1;
                return 49.0 * ( dot(m0*m0, vec3( dot( p0, x0 ), dot( p1, x1 ), dot( p2, x2 )))
                + dot(m1*m1, vec2( dot( p3, x3 ), dot( p4, x4 ) ) ) ) ;
            }       

            vec3 toFireColor(float n) {

                n *= 0.8;

                float red = clamp(3.0*n, 0.0, 1.0);
                float grn = clamp(3.0*n-1.0, 0.0, 1.0);
                float blu = clamp(3.0*n-2.0, 0.0, 1.0);

                return vec3(red,grn,blu);
            }

            void main() {
                float t = time;
                float alpha = 0.5;

                // Wind in x-position
                float windspeedX = 1.0 + 0.1*max(0.0,st.y);
                float xValue = (ewWind != 0.0)?(ewWind*(windspeedX*windspeedX-1.0))*(st.y*0.005):0.0;

                // Wind in z-position
                float windspeedZ = 1.0 + 0.1*max(0.0,st.y);
                float zValue = (snWind != 0.0)?(snWind*(windspeedZ*windspeedZ-1.0))*(st.y*0.005):0.0;

                float n = 0.0;
                n += (1.0/1.0)      * abs(snoise(vec4(1.0*st.x - xValue*t,     0.25*st.y-1.0*t,    1.0*st.z - zValue * t,   t*0.5)));
                n += (1.0/2.0)      * abs(snoise(vec4(2.0*st.x - xValue*t,     0.5*st.y-1.0*t,     2.0*st.z - zValue * t,   t*0.5)));
                n += (1.0/4.0)      * abs(snoise(vec4(4.0*st.x - xValue*t,     0.75*st.y-1.0*t,    4.0*st.z - zValue * t,   t*0.5)));
                n += (1.0/8.0)      * abs(snoise(vec4(8.0*st.x - xValue*t,     1.0*st.y-1.0*t,     8.0*st.z - zValue * t,   t*0.5)));
                n += (1.0/16.0)     * abs(snoise(vec4(16.0*st.x - xValue*t,    2.0*st.y-2.0*t,     16.0*st.z - zValue * t,  t*0.5)));
                n += (1.0/32.0)     * abs(snoise(vec4(32.0*st.x - xValue*t,    3.0*st.y-3.0*t,     32.0*st.z - zValue * t,  t*0.5)));
                n += (1.0/64.0)     * abs(snoise(vec4(64.0*st.x - xValue*t,    4.0*st.y-3.0*t,     64.0*st.z - zValue * t,  t*0.5)));
                n += (1.0/128.0)    * abs(snoise(vec4(128.0*st.x - xValue*t,   5.0*st.y-3.0*t,     128.0*st.z - zValue * t, t*0.5)));

                //n *= sqrt(2.0 - st.y+0.5);
                alpha = (n < 0.3)?0.0:alpha;

                gl_FragColor = vec4(toFireColor(n), alpha);
            }
        </script>

        <script type="x-shader/x-vertex" id="smokeVertexshader">

            #ifdef GL_ES
                precision highp float;
            #endif

            varying vec3 st;

            void main(void) {
                st = position;
                gl_Position = projectionMatrix * modelViewMatrix * vec4( position, 1.0 );   
            }
        </script>

        <script type="x-shader/x-fragment" id="smokeFragmentshader">

            #ifdef GL_ES
                precision highp float;
            #endif

            varying vec3 st; // Texcoords
            uniform float time; 

            //
            // Description : Array and textureless GLSL 2D/3D/4D simplex 
            //               noise functions.
            //      Author : Ian McEwan, Ashima Arts.
            //  Maintainer : ijm
            //     Lastmod : 20110822 (ijm)
            //     License : Copyright (C) 2011 Ashima Arts. All rights reserved.
            //               Distributed under the MIT License. See LICENSE file.
            //               https://github.com/ashima/webgl-noise
            // 

            vec3 mod289(vec3 x) {
            return x - floor(x * (1.0 / 289.0)) * 289.0;
            }

            vec4 mod289(vec4 x) {
            return x - floor(x * (1.0 / 289.0)) * 289.0;
            }

            vec4 permute(vec4 x) {
            return mod289(((x*34.0)+1.0)*x);
            }

            vec4 taylorInvSqrt(vec4 r)
            {
            return 1.79284291400159 - 0.85373472095314 * r;
            }

            float snoise(vec3 v)
            { 
            const vec2  C = vec2(1.0/6.0, 1.0/3.0) ;
            const vec4  D = vec4(0.0, 0.5, 1.0, 2.0);

            // First corner
            vec3 i  = floor(v + dot(v, C.yyy) );
            vec3 x0 =   v - i + dot(i, C.xxx) ;

            // Other corners
            vec3 g = step(x0.yzx, x0.xyz);
            vec3 l = 1.0 - g;
            vec3 i1 = min( g.xyz, l.zxy );
            vec3 i2 = max( g.xyz, l.zxy );

            //   x0 = x0 - 0.0 + 0.0 * C.xxx;
            //   x1 = x0 - i1  + 1.0 * C.xxx;
            //   x2 = x0 - i2  + 2.0 * C.xxx;
            //   x3 = x0 - 1.0 + 3.0 * C.xxx;
            vec3 x1 = x0 - i1 + C.xxx;
            vec3 x2 = x0 - i2 + C.yyy; // 2.0*C.x = 1/3 = C.y
            vec3 x3 = x0 - D.yyy;      // -1.0+3.0*C.x = -0.5 = -D.y

            // Permutations
            i = mod289(i); 
            vec4 p = permute( permute( permute( 
            i.z + vec4(0.0, i1.z, i2.z, 1.0 ))
            + i.y + vec4(0.0, i1.y, i2.y, 1.0 )) 
            + i.x + vec4(0.0, i1.x, i2.x, 1.0 ));

            // Gradients: 7x7 points over a square, mapped onto an octahedron.
            // The ring size 17*17 = 289 is close to a multiple of 49 (49*6 = 294)
            float n_ = 0.142857142857; // 1.0/7.0
            vec3  ns = n_ * D.wyz - D.xzx;

            vec4 j = p - 49.0 * floor(p * ns.z * ns.z);  //  mod(p,7*7)

            vec4 x_ = floor(j * ns.z);
            vec4 y_ = floor(j - 7.0 * x_ );    // mod(j,N)

            vec4 x = x_ *ns.x + ns.yyyy;
            vec4 y = y_ *ns.x + ns.yyyy;
            vec4 h = 1.0 - abs(x) - abs(y);

            vec4 b0 = vec4( x.xy, y.xy );
            vec4 b1 = vec4( x.zw, y.zw );

            //vec4 s0 = vec4(lessThan(b0,0.0))*2.0 - 1.0;
            //vec4 s1 = vec4(lessThan(b1,0.0))*2.0 - 1.0;
            vec4 s0 = floor(b0)*2.0 + 1.0;
            vec4 s1 = floor(b1)*2.0 + 1.0;
            vec4 sh = -step(h, vec4(0.0));

            vec4 a0 = b0.xzyw + s0.xzyw*sh.xxyy ;
            vec4 a1 = b1.xzyw + s1.xzyw*sh.zzww ;

            vec3 p0 = vec3(a0.xy,h.x);
            vec3 p1 = vec3(a0.zw,h.y);
            vec3 p2 = vec3(a1.xy,h.z);
            vec3 p3 = vec3(a1.zw,h.w);

            //Normalise gradients
            vec4 norm = taylorInvSqrt(vec4(dot(p0,p0), dot(p1,p1), dot(p2, p2), dot(p3,p3)));
            p0 *= norm.x;
            p1 *= norm.y;
            p2 *= norm.z;
            p3 *= norm.w;

            // Mix final noise value
            vec4 m = max(0.6 - vec4(dot(x0,x0), dot(x1,x1), dot(x2,x2), dot(x3,x3)), 0.0);
            m = m * m;
            return 42.0 * dot( m*m, vec4( dot(p0,x0), dot(p1,x1), 
                    dot(p2,x2), dot(p3,x3) ) );
            }
            //
            // GLSL implementation of 2D "flow noise" as presented
            // by Ken Perlin and Fabrice Neyret at Siggraph 2001.
            // (2D simplex noise with analytic derivatives and
            // in-plane rotation of generating gradients,
            // in a fractal sum where higher frequencies are
            // displaced (advected) by lower frequencies in the
            // direction of their gradient. For details, please
            // refer to the 2001 paper "Flow Noise" by Perlin and Neyret.)
            //
            // Author: Stefan Gustavson (stefan.gustavson@liu.se)
            // Distributed under the terms of the MIT license.
            // See LICENSE file for details.
            // 

            // Helper constants
            #define F2 0.366025403
            #define G2 0.211324865
            #define K 0.0243902439 // 1/41

            // Permutation polynomial
            float permute(float x) {
            return mod((34.0 * x + 1.0)*x, 289.0);
            }

            // Gradient mapping with an extra rotation.
            vec2 grad2(vec2 p, float rot) {
            #if 1
            // Map from a line to a diamond such that a shift maps to a rotation.
            float u = permute(permute(p.x) + p.y) * K + rot; // Rotate by shift
            u = 4.0 * fract(u) - 2.0;
            return vec2(abs(u)-1.0, abs(abs(u+1.0)-2.0)-1.0);
            #else
            #define TWOPI 6.28318530718
            // For more isotropic gradients, sin/cos can be used instead.
            float u = permute(permute(p.x) + p.y) * K + rot; // Rotate by shift
            u = fract(u) * TWOPI;
            return vec2(cos(u), sin(u));
            #endif
            }

            float srdnoise(in vec2 P, in float rot, out vec2 grad) {

            // Transform input point to the skewed simplex grid
            vec2 Ps = P + dot(P, vec2(F2));

            // Round down to simplex origin
            vec2 Pi = floor(Ps);

            // Transform simplex origin back to (x,y) system
            vec2 P0 = Pi - dot(Pi, vec2(G2));

            // Find (x,y) offsets from simplex origin to first corner
            vec2 v0 = P - P0;

            // Pick (+x, +y) or (+y, +x) increment sequence
            vec2 i1 = (v0.x > v0.y) ? vec2(1.0, 0.0) : vec2 (0.0, 1.0);

            // Determine the offsets for the other two corners
            vec2 v1 = v0 - i1 + G2;
            vec2 v2 = v0 - 1.0 + 2.0 * G2;

            // Wrap coordinates at 289 to avoid float precision problems
            Pi = mod(Pi, 289.0);

            // Calculate the circularly symmetric part of each noise wiggle
            vec3 t = max(0.5 - vec3(dot(v0,v0), dot(v1,v1), dot(v2,v2)), 0.0);
            vec3 t2 = t*t;
            vec3 t4 = t2*t2;

            // Calculate the gradients for the three corners
            vec2 g0 = grad2(Pi, rot);
            vec2 g1 = grad2(Pi + i1, rot);
            vec2 g2 = grad2(Pi + 1.0, rot);

            // Compute noise contributions from each corner
            vec3 gv = vec3(dot(g0,v0), dot(g1,v1), dot(g2,v2)); // ramp: g dot v
            vec3 n = t4 * gv;  // Circular kernel times linear ramp

            // Compute partial derivatives in x and y
            vec3 temp = t2 * t * gv;
            vec3 gradx = temp * vec3(v0.x, v1.x, v2.x);
            vec3 grady = temp * vec3(v0.y, v1.y, v2.y);
            grad.x = -8.0 * (gradx.x + gradx.y + gradx.z);
            grad.y = -8.0 * (grady.x + grady.y + grady.z);
            grad.x += dot(t4, vec3(g0.x, g1.x, g2.x));
            grad.y += dot(t4, vec3(g0.y, g1.y, g2.y));
            grad *= 40.0;

            // Add contributions from the three corners and return
            return 40.0 * (n.x + n.y + n.z);
            }  

            float clamps(float value, float minValue, float maxValue) {
                return max(minValue, min(value, maxValue));
            }
            float smoothsteps(float edge0, float edge1, float value) {
                float t = clamps((value - edge0) / (edge1 - edge0), 0.0, 1.0);
                return t * t * (3.0 - 2.0 * t);
            }

            void main() {
                float t = time;
                float alpha = 0.1;
                vec2 gradient;

                float n = 0.0;

                n +=  (1.0/1.0) * snoise(vec3(1.0*st.x,     1.0*(st.y-1.0*t),   1.0*st.z));
                n +=  (1.0/2.0) * snoise(vec3(2.0*st.x,     2.0*(st.y-1.0*t),   2.0*st.z));
                n +=  (1.0/4.0) * snoise(vec3(4.0*st.x,     4.0*(st.y-1.0*t),   4.0*st.z));
                n +=  (1.0/8.0) * snoise(vec3(8.0*st.x,     8.0*(st.y-1.0*t),   8.0*st.z));
                n +=  (1.0/16.0) * snoise(vec3(16.0*st.x,   16.0*(st.y-1.0*t),  16.0*st.z));
                n +=  (1.0/32.0) * snoise(vec3(32.0*st.x,   32.0*(st.y-1.0*t),  32.0*st.z));
                n +=  (1.0/64.0) * snoise(vec3(64.0*st.x,   64.0*(st.y-1.0*t),  64.0*st.z));
                n +=  (1.0/128.0) * snoise(vec3(128.0*st.x, 128.0*(st.y-1.0*t), 128.0*st.z));

                // n *= sqrt(3.6 - st.y);
                n *= cos(st.x*srdnoise(vec2(1.0*st.x, 1.0*st.y), 0.1*t, gradient)*5.0);

                n *= smoothsteps(3.0, 1.7, st.x) * smoothsteps(-3.0, -1.5, st.x);

                gl_FragColor = vec4(n,n,n, alpha);
            }
        </script>

        <script type="x-shader/x-vertex" id="ballVertexshader">

            #ifdef GL_ES
                precision highp float;
            #endif

            varying vec3 st;
            uniform float time; 

            void main(void) {
                st = position;
                gl_Position = projectionMatrix * modelViewMatrix * vec4( position, 1.0 );   
            }
        </script>

        <script type="x-shader/x-fragment" id="ballFragmentshader">

            #ifdef GL_ES
                precision highp float;
            #endif

            varying vec3 st; // Texcoords
            uniform float time;


            // Cellular noise ("Worley noise") in 3D in GLSL.
            // Copyright (c) Stefan Gustavson 2011-04-19. All rights reserved.
            // This code is released under the conditions of the MIT license.
            // See LICENSE file for details.

            // Permutation polynomial: (34x^2 + x) mod 289
            vec3 permute(vec3 x) {
              return mod((34.0 * x + 1.0) * x, 289.0);
            }

            // Cellular noise, returning F1 and F2 in a vec2.
            // 3x3x3 search region for good F2 everywhere, but a lot
            // slower than the 2x2x2 version.
            // The code below is a bit scary even to its author,
            // but it has at least half decent performance on a
            // modern GPU. In any case, it beats any software
            // implementation of Worley noise hands down.

            vec2 cellular(vec3 P) {
                #define K 0.142857142857 // 1/7
                #define Ko 0.428571428571 // 1/2-K/2
                #define K2 0.020408163265306 // 1/(7*7)
                #define Kz 0.166666666667 // 1/6
                #define Kzo 0.416666666667 // 1/2-1/6*2
                #define jitter 1.5 // smaller jitter gives more regular pattern

                vec3 Pi = mod(floor(P), 289.0);
                vec3 Pf = fract(P) - 0.5;

                vec3 Pfx = Pf.x + vec3(1.0, 0.0, -1.0);
                vec3 Pfy = Pf.y + vec3(1.0, 0.0, -1.0);
                vec3 Pfz = Pf.z + vec3(1.0, 0.0, -1.0);

                vec3 p = permute(Pi.x + vec3(-1.0, 0.0, 1.0));
                vec3 p1 = permute(p + Pi.y - 1.0);
                vec3 p2 = permute(p + Pi.y);
                vec3 p3 = permute(p + Pi.y + 1.0);

                vec3 p11 = permute(p1 + Pi.z - 1.0);
                vec3 p12 = permute(p1 + Pi.z);
                vec3 p13 = permute(p1 + Pi.z + 1.0);

                vec3 p21 = permute(p2 + Pi.z - 1.0);
                vec3 p22 = permute(p2 + Pi.z);
                vec3 p23 = permute(p2 + Pi.z + 1.0);

                vec3 p31 = permute(p3 + Pi.z - 1.0);
                vec3 p32 = permute(p3 + Pi.z);
                vec3 p33 = permute(p3 + Pi.z + 1.0);

                vec3 ox11 = fract(p11*K) - Ko;
                vec3 oy11 = mod(floor(p11*K), 7.0)*K - Ko;
                vec3 oz11 = floor(p11*K2)*Kz - Kzo; // p11 < 289 guaranteed

                vec3 ox12 = fract(p12*K) - Ko;
                vec3 oy12 = mod(floor(p12*K), 7.0)*K - Ko;
                vec3 oz12 = floor(p12*K2)*Kz - Kzo;

                vec3 ox13 = fract(p13*K) - Ko;
                vec3 oy13 = mod(floor(p13*K), 7.0)*K - Ko;
                vec3 oz13 = floor(p13*K2)*Kz - Kzo;

                vec3 ox21 = fract(p21*K) - Ko;
                vec3 oy21 = mod(floor(p21*K), 7.0)*K - Ko;
                vec3 oz21 = floor(p21*K2)*Kz - Kzo;

                vec3 ox22 = fract(p22*K) - Ko;
                vec3 oy22 = mod(floor(p22*K), 7.0)*K - Ko;
                vec3 oz22 = floor(p22*K2)*Kz - Kzo;

                vec3 ox23 = fract(p23*K) - Ko;
                vec3 oy23 = mod(floor(p23*K), 7.0)*K - Ko;
                vec3 oz23 = floor(p23*K2)*Kz - Kzo;

                vec3 ox31 = fract(p31*K) - Ko;
                vec3 oy31 = mod(floor(p31*K), 7.0)*K - Ko;
                vec3 oz31 = floor(p31*K2)*Kz - Kzo;

                vec3 ox32 = fract(p32*K) - Ko;
                vec3 oy32 = mod(floor(p32*K), 7.0)*K - Ko;
                vec3 oz32 = floor(p32*K2)*Kz - Kzo;

                vec3 ox33 = fract(p33*K) - Ko;
                vec3 oy33 = mod(floor(p33*K), 7.0)*K - Ko;
                vec3 oz33 = floor(p33*K2)*Kz - Kzo;

                vec3 dx11 = Pfx + jitter*ox11;
                vec3 dy11 = Pfy.x + jitter*oy11;
                vec3 dz11 = Pfz.x + jitter*oz11;

                vec3 dx12 = Pfx + jitter*ox12;
                vec3 dy12 = Pfy.x + jitter*oy12;
                vec3 dz12 = Pfz.y + jitter*oz12;

                vec3 dx13 = Pfx + jitter*ox13;
                vec3 dy13 = Pfy.x + jitter*oy13;
                vec3 dz13 = Pfz.z + jitter*oz13;

                vec3 dx21 = Pfx + jitter*ox21;
                vec3 dy21 = Pfy.y + jitter*oy21;
                vec3 dz21 = Pfz.x + jitter*oz21;

                vec3 dx22 = Pfx + jitter*ox22;
                vec3 dy22 = Pfy.y + jitter*oy22;
                vec3 dz22 = Pfz.y + jitter*oz22;

                vec3 dx23 = Pfx + jitter*ox23;
                vec3 dy23 = Pfy.y + jitter*oy23;
                vec3 dz23 = Pfz.z + jitter*oz23;

                vec3 dx31 = Pfx + jitter*ox31;
                vec3 dy31 = Pfy.z + jitter*oy31;
                vec3 dz31 = Pfz.x + jitter*oz31;

                vec3 dx32 = Pfx + jitter*ox32;
                vec3 dy32 = Pfy.z + jitter*oy32;
                vec3 dz32 = Pfz.y + jitter*oz32;

                vec3 dx33 = Pfx + jitter*ox33;
                vec3 dy33 = Pfy.z + jitter*oy33;
                vec3 dz33 = Pfz.z + jitter*oz33;

                vec3 d11 = dx11 * dx11 + dy11 * dy11 + dz11 * dz11;
                vec3 d12 = dx12 * dx12 + dy12 * dy12 + dz12 * dz12;
                vec3 d13 = dx13 * dx13 + dy13 * dy13 + dz13 * dz13;
                vec3 d21 = dx21 * dx21 + dy21 * dy21 + dz21 * dz21;
                vec3 d22 = dx22 * dx22 + dy22 * dy22 + dz22 * dz22;
                vec3 d23 = dx23 * dx23 + dy23 * dy23 + dz23 * dz23;
                vec3 d31 = dx31 * dx31 + dy31 * dy31 + dz31 * dz31;
                vec3 d32 = dx32 * dx32 + dy32 * dy32 + dz32 * dz32;
                vec3 d33 = dx33 * dx33 + dy33 * dy33 + dz33 * dz33;

                // Sort out the two smallest distances (F1, F2)
            #if 0
                // Cheat and sort out only F1
                vec3 d1 = min(min(d11,d12), d13);
                vec3 d2 = min(min(d21,d22), d23);
                vec3 d3 = min(min(d31,d32), d33);
                vec3 d = min(min(d1,d2), d3);
                d.x = min(min(d.x,d.y),d.z);
                return sqrt(d.xx); // F1 duplicated, no F2 computed
            #else
                // Do it right and sort out both F1 and F2
                vec3 d1a = min(d11, d12);
                d12 = max(d11, d12);
                d11 = min(d1a, d13); // Smallest now not in d12 or d13
                d13 = max(d1a, d13);
                d12 = min(d12, d13); // 2nd smallest now not in d13
                vec3 d2a = min(d21, d22);
                d22 = max(d21, d22);
                d21 = min(d2a, d23); // Smallest now not in d22 or d23
                d23 = max(d2a, d23);
                d22 = min(d22, d23); // 2nd smallest now not in d23
                vec3 d3a = min(d31, d32);
                d32 = max(d31, d32);
                d31 = min(d3a, d33); // Smallest now not in d32 or d33
                d33 = max(d3a, d33);
                d32 = min(d32, d33); // 2nd smallest now not in d33
                vec3 da = min(d11, d21);
                d21 = max(d11, d21);
                d11 = min(da, d31); // Smallest now in d11
                d31 = max(da, d31); // 2nd smallest now not in d31
                d11.xy = (d11.x < d11.y) ? d11.xy : d11.yx;
                d11.xz = (d11.x < d11.z) ? d11.xz : d11.zx; // d11.x now smallest
                d12 = min(d12, d21); // 2nd smallest now not in d21
                d12 = min(d12, d22); // nor in d22
                d12 = min(d12, d31); // nor in d31
                d12 = min(d12, d32); // nor in d32
                d11.yz = min(d11.yz,d12.xy); // nor in d12.yz
                d11.y = min(d11.y,d12.z); // Only two more to go
                d11.y = min(d11.y,d11.z); // Done! (Phew!)
                return sqrt(d11.xy); // F1, F2
            #endif
            }

            vec3 toFireColor(float n) {

                n *= 0.8;

                float red = clamp(3.0*n, 0.0, 1.0);
                float grn = clamp(3.0*n-1.0, 0.0, 1.0);
                float blu = clamp(3.0*n-2.0, 0.0, 1.0);

                return vec3(red,grn,blu);
            }

            void main(void) {
                vec2 F = cellular(vec3(st.x*3.0, st.y*3.0-time*0.2, st.z*3.0-time*0.2));
                float n = F.y-F.x;

                n = (n > 0.05)?n:0.0;

                gl_FragColor = vec4(toFireColor(n), 1.0);
            }
        </script>

        <script>
            var scene, camera, renderer, fireball, plane, uniform, controls;
            var container, stats;

            init();
            animate();

            function init() {

                stats = new Stats();
                stats.setMode( 0 ); // 0: fps, 1: ms, 2: mb

                // align top-left
                stats.domElement.style.position = 'absolute';
                stats.domElement.style.left = '0px';
                stats.domElement.style.top = '0px';


                container = document.createElement( 'div' );
                var fireballWindow = document.getElementById('fireball');
                fireballWindow.appendChild( container );
                document.body.appendChild( stats.domElement ); 

                /*
                 * Create the scene and set spec.
                 */
                scene = new THREE.Scene();
                camera = new THREE.PerspectiveCamera( 60, $('#fireball').width() / $('#fireball').height(), 1, 1000);
                camera.position.z = 20;

                renderer = new THREE.WebGLRenderer();
                renderer.setPixelRatio( $('#fireball').devicePixelRatio );
                renderer.setSize( $('#fireball').width(), $('#fireball').height());
                container.appendChild( renderer.domElement );

                /*
                * Set config for the controls
                */
                controls = new THREE.OrbitControls( camera, renderer.domElement );
                controls.enableDamping = true;
                controls.dampingFactor = 0.25;
                controls.enableZoom = false;

                /*
                * Attributes to shaders
                */
                uniform = {
                    time:   { type: "f", value: 1.0 },
                    ewWind: { type: "f", value: 0.0 },
                    snWind: { type: "f", value: 0.0 },
                }

                /* 
                 *   Create a inner sphere
                 */
                var geometry = new THREE.SphereGeometry( 2.8, 500, 500 );
                // Set material for the inner sphere
                var shaderMaterial =
                  new THREE.ShaderMaterial({
                    uniforms: uniform,
                    vertexShader:   document.getElementById('ballVertexshader').innerHTML,
                    fragmentShader: document.getElementById('ballFragmentshader').innerHTML,
                    blending: THREE['AdditiveBlending']
                  });
                var innerBall = new THREE.Mesh(geometry, shaderMaterial);
                innerBall.position.x = 0;
                innerBall.position.y = -2;
                innerBall.position.z = 0;
                scene.add( innerBall );

                /* 
                 *   Create a burning sphere
                 */
                //Set material for the burning
                var fireMaterial =
                  new THREE.ShaderMaterial({
                    uniforms: uniform,
                    vertexShader:   document.getElementById('fireVertexshader').innerHTML,
                    fragmentShader: document.getElementById('fireFragmentshader').innerHTML,
                    transparent: true,
                    blending: THREE['AdditiveBlending'],
                    //depthTest: false,
                  });

                for(var i = 2.8; i < 3.2; i+=0.1) {
                    var geometryFireball = new THREE.SphereGeometry( i, 150, 150);
                    var fireball = new THREE.Mesh(geometryFireball, fireMaterial);
                    fireball.position.x = 0;
                    fireball.position.y = -0.5;
                    fireball.position.z = 0;
                    scene.add( fireball );
                }
                

                /*
                *   Cylinders WITH SMOKE
                */
                var smokeShader =
                  new THREE.ShaderMaterial({
                    uniforms: uniform,
                    vertexShader:   document.getElementById('smokeVertexshader').innerHTML,
                    fragmentShader: document.getElementById('smokeFragmentshader').innerHTML,
                    transparent: true,
                    side: THREE.DoubleSide,
                    depthWrite: false,
                    blending: THREE['AdditiveBlending']
                  });

                var planeGeometry1 = new THREE.BoxGeometry( 5, 15, 0.01 );
                var plane1 = new THREE.Mesh( planeGeometry1, smokeShader );
                plane1.position.y = 5;
                scene.add( plane1 );

                var planeGeometry2 = new THREE.BoxGeometry( 5, 15, 0.01 );
                var plane2 = new THREE.Mesh( planeGeometry2, smokeShader );
                plane2.rotation.y = 1;
                plane2.position.y = 5;
                scene.add( plane2 );

                var planeGeometry3 = new THREE.BoxGeometry( 5, 15, 0.01 );
                var plane3 = new THREE.Mesh( planeGeometry3, smokeShader );
                plane3.rotation.y = 2;
                plane3.position.y = 5.0;
                scene.add( plane3 );

                /*
                *   LIGHTS
                */

                var light = new THREE.AmbientLight( 0x404040 ); // soft white light
                scene.add( light )

                pointLight = new THREE.PointLight( 0x6b7a82, 0.5, 100 );
                pointLight.position.set( 20, 20, 30 );
                scene.add( pointLight );

            };

            function onWindowResize() {

                camera.aspect = window.innerWidth / window.innerHeight;
                camera.updateProjectionMatrix();
                renderer.setSize( window.innerWidth, window.innerHeight );
            }

            function animate() {
                requestAnimationFrame( animate );
                controls.update(); // required if controls.enableDamping = true, or if controls.autoRotate = true
                render();
            }

            function render() {
                stats.begin();
                uniform.time.value += 0.03;
                uniform.ewWind.value = parseFloat(document.getElementById("west-east-wind").value).toFixed(2);
                uniform.snWind.value = parseFloat(document.getElementById("south-north-wind").value).toFixed(2);
                renderer.render( scene, camera );
                stats.end();
            }
        </script>
    </body>
</html>